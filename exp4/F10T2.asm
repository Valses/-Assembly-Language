
INCLUDE MYLIB.LIB

PUBLIC   F10T2
EXTRN  BUF:BYTE,PP:WORD
;子程序名字:F10T2
;功能:将以si为指针的字节存储区中的有符号十进制数字串转换成二进制数送入AX中
;入口参数:SI :指向字节存储区的首址
;          CX  :存放待转换十进制数字的长度，不包括正负号
;出口参数:	SI:为-1的时候表示输入有错误
;	    AX:存放转换以后的子数据
;所用寄存器:EBX:存放正在被转换的数字
	
.386
DATA SEGMENT USE16 PARA PUBLIC 'DATA'
	SIGNN DB 0
DATA ENDS
CODE SEGMENT USE16 PARA PUBLIC 'CODE'
	ASSUME CS:CODE,DS:DATA
F10T2 PROC NEAR
	PUSH EBX
	MOV EAX,0
	MOV SIGNN,0
	MOV BL,[SI]
	CMP BL,'+'
	JE POS
	CMP BL,'-'
	JNE NUM
	MOV SIGNN,1
POS:	OR CX,CX
	JZ ERR1
NEXT1:	INC SI
	MOV BL,[SI]
NUM:	CMP BL,'0'
	JB ERR1
	CMP BL,'9'
	JA ERR1
	SUB BL,30H
	MOVZX EBX,BL
	IMUL EAX,10
	JO ERR1
	ADD EAX,EBX
	JO ERR1
	JS ERR1
	JC ERR1
	DEC CX
	JNZ NEXT1
	CMP EAX,7FFFH
	JA ERR1
	CMP SIGNN,1
	JNE OVE
	NEG EAX
OVE:	POP EBX	
	RET
ERR1:	MOV SI,-1
	JMP OVE
F10T2 ENDP
CODE ENDS
	END
